//打包类型
plugins {
    id 'com.android.application'
}
android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "com.aige.loveproduction"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 3
        versionName "3.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        //声明多渠道打包风味维度
        flavorDimensions "channel"
        //混淆配置
        proguardFiles 'proguard-sdk.pro', 'proguard-rules.pro'

        vectorDrawables.useSupportLibrary = true
        //X5兼容64位手机
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    //java版本
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    // 设置存放 so 文件的目录
    sourceSets {
        main {
            jniLibs.srcDirs = ['armeabi']
        }
    }
    //打包签名
    signingConfigs {
        config {
            storeFile file('../app/src/main/filters/sign/loveproduction.jks')
            storePassword 'zwk.2020452796'
            keyAlias 'key0'
            keyPassword 'zwk.2020452796'
        }
    }
    //构建打包类型
    buildTypes {
        debug {
            debuggable true
            //开启混淆开关
            minifyEnabled false
            //移除无用的resource文件开关
            zipAlignEnabled false
            //压缩对齐开关
            zipAlignEnabled true
            //为BuildConfig.class文件添加新属性，指定主机服务器地址
            buildConfigField 'String','SERVER',getSERVER('debug','SERVER')
            buildConfigField 'String','SERVER_ONE',getSERVER('debug','SERVER_ONE')
            //签名配置信息
            signingConfig signingConfigs.config
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField 'String','SERVER',getSERVER('release','SERVER')
            buildConfigField 'String','SERVER_ONE',getSERVER('release',"SERVER_ONE")
            signingConfig signingConfigs.config
        }
    }

    //输出apk
    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                outputFileName = rootProject.getName()+"_v"+variant.versionName+"_"+variant.buildType.name+".apk"
            }
    }

    //多渠道打包；运行时可在左边Build Variants选择要运行的版本
    productFlavors {
        xiaomi{
            //为BuildConfig.class文件添加新属性
            buildConfigField 'String','PLATE_FROM','"xiaomi"'
            manifestPlaceholders = [UMENG_CHANNEL_VALUE:"xiaomi"]
            dimension "channel"
        }
        yingyongbao{
            buildConfigField 'String','PLATE_FROM','"yingyongbao"'
            manifestPlaceholders = [UMENG_CHANNEL_VALUE:"yingyongbao"]
            dimension "channel"
        }
    }

}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //二维码扫描
    //implementation('cn.yipianfengye.android:zxing-library:2.2')
    implementation('com.journeyapps:zxing-android-embedded:3.5.0')

    //okhttp3
    implementation 'com.squareup.okhttp3:okhttp:4.2.0'
    implementation "com.squareup.okhttp3:logging-interceptor:3.10.0"
    /**
     * Retrofit+RxJava+Gson，RxJava和Gson都自带Retrofit，所以只需要引入RxJava和Gson即可
     */
    //Gson用于对请求返回的数据与实体Bean解析，由原本Call<ResponseBody>只能返回ResponseBody变成Call<Bean>
    //只要构建与返回数据一一对应的Bean类就可自动转换成相应的Bean对象
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    //RxJava用于对请求返回的数据进行封装，由原Call<ResponseBody>格式可装换为入Observable<ResponseBody>多种格式的返回数据
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'
    //运行时权限
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'


    //AutoDispose解决RxJava内存泄漏
    implementation 'com.uber.autodispose2:autodispose:2.0.0'
    implementation 'com.uber.autodispose2:autodispose-android:2.0.0'
    implementation 'com.uber.autodispose2:autodispose-lifecycle:2.0.0'
    implementation 'com.uber.autodispose2:autodispose-androidx-lifecycle:2.0.0'

    //注解库，可限定一些变量、方法参数或者是方法返回值的范围，比如@IdRes只能填入R.id.?的值
    implementation 'com.android.support:support-annotations:28.0.0'

    //权限请求框架
//    implementation 'com.github.hotchemi:permissionsdispatcher:2.3.1'
//    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:2.3.1'

    //lottie动画框架，可使用json文件播放动画
    implementation 'com.airbnb.android:lottie:3.6.1'

    //腾讯SDK
    //api 'com.tencent.tbs:tbssdk:44085'

    //MPAndroidChart图表
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    //图片加载框架
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
    //图片预览
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    }
//获取配置文件中的内容
def getSERVER(String context,String Server) {
    def SERVER = null
    Properties properties = new Properties()
    def proFile = file("src/main/filters/"+context+"/config.properties")
    if(proFile.canRead()) {
        properties.load(new FileInputStream(proFile))
        if(properties != null) {
            SERVER = properties[Server]
        }
    }
    return SERVER
}